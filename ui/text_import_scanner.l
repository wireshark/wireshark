/* -*-mode: flex-*- */

%top {
/* Include this before everything else, for various large-file definitions */
#include "config.h"
#include <wireshark.h>
}

/*
 * We want a reentrant scanner.
 */
%option reentrant

/*
 * We don't use input, so don't generate code for it.
 */
%option noinput

/*
 * We don't use unput, so don't generate code for it.
 */
%option nounput

/*
 * We don't read interactively from the terminal.
 */
%option never-interactive

/*
 * We want to stop processing when we get to the end of the input.
 */
%option noyywrap

/*
 * Prefix scanner routines with "text_import_" rather than "yy", so this scanner
 * can coexist with other scanners.
 */
%option prefix="text_import_"

/*
 * We have to override the memory allocators so that we don't get
 * "unused argument" warnings from the yyscanner argument (which
 * we don't use, as we have a global memory allocator).
 *
 * We provide, as macros, our own versions of the routines generated by Flex,
 * which just call malloc()/realloc()/free() (as the Flex versions do),
 * discarding the extra argument.
 */
%option noyyalloc
%option noyyrealloc
%option noyyfree

%{

/********************************************************************************
 *
 * text_import_scanner.l
 * Scanner for text import
 * November 2010, Jaap Keuter <jaap.keuter@xs4all.nl>
 *
 * Wireshark - Network traffic analyzer
 * By Gerald Combs <gerald@wireshark.org>
 * Copyright 1998 Gerald Combs
 *
 * Based on text2pcap-scanner.l by Ashok Narayanan <ashokn@cisco.com>
 *
 * SPDX-License-Identifier: GPL-2.0-or-later
 *
 *******************************************************************************/

#include <stdio.h>
#include <stdlib.h>

#include "text_import_scanner.h"

/*
 * Disable diagnostics in the code generated by Flex.
 */
DIAG_OFF_FLEX()

/*
 * Flex (v 2.5.35) uses this symbol to "exclude" unistd.h
 */
#ifdef _WIN32
#  define YY_NO_UNISTD_H
#endif

/*
 * Sleazy hack to suppress compiler warnings in yy_fatal_error().
 */
#define YY_EXIT_FAILURE ((void)yyscanner, 2)

/*
 * Macros for the allocators, to discard the extra argument.
 */
#define text_import_alloc(size, yyscanner)		(void *)malloc(size)
#define text_import_realloc(ptr, size, yyscanner)	(void *)realloc((char *)(ptr), (size))
#define text_import_free(ptr, yyscanner)		free((char *)ptr)

%}

directive ^#TEXT2PCAP.*\r?\n
comment ^[\t ]*#.*\r?\n
byte [[:xdigit:]]{2}
offset (0[xX])?[[:xdigit:]]{3,8}:?
text [^ \r\n\t]+
mailfwd >
mailspace [> \t]
eol \r?\n\r?

%%

{byte}            { if (parse_token(T_BYTE, yytext) != IMPORT_SUCCESS) return IMPORT_FAILURE; }
{byte}{2,4}       { if (parse_token(T_BYTES, yytext) != IMPORT_SUCCESS) return IMPORT_FAILURE; }
{offset}          { if (parse_token(T_OFFSET, yytext) != IMPORT_SUCCESS) return IMPORT_FAILURE; }
{mailfwd}+{offset} {
	/* Handle the case where no blanks are between mailfwd and offset */
	if (parse_token(T_OFFSET, yytext + strspn(yytext, ">")) != IMPORT_SUCCESS) return IMPORT_FAILURE;
}
{eol}             { if (parse_token(T_EOL, NULL) != IMPORT_SUCCESS) return IMPORT_FAILURE; }
^{mailspace}+     ; /* ignore mailfwd characters (incl. separated by space) at line start */
[ \t]             ; /* ignore whitespace */
{directive}       { if (parse_token(T_DIRECTIVE, yytext) != IMPORT_SUCCESS) return IMPORT_FAILURE;
	if (parse_token(T_EOL, NULL) != IMPORT_SUCCESS) return IMPORT_FAILURE; }
{comment}         { if (parse_token(T_EOL, NULL) != IMPORT_SUCCESS) return IMPORT_FAILURE; }
{text}            { if (parse_token(T_TEXT, yytext) != IMPORT_SUCCESS) return IMPORT_FAILURE; }

<<EOF>>           { if (parse_token(T_EOF, NULL) != IMPORT_SUCCESS) return IMPORT_FAILURE; yyterminate(); }

%%

/*
 * Turn diagnostics back on, so we check the code that we've written.
 */
DIAG_ON_FLEX()

import_status_t
text_import_scan(FILE *input_file)
{
    yyscan_t scanner;
    int ret;

    if (text_import_lex_init(&scanner) != 0)
        return IMPORT_INIT_FAILED;

    text_import_set_in(input_file, scanner);

    ret = text_import_lex(scanner);

    text_import_lex_destroy(scanner);

    return ret;
}
